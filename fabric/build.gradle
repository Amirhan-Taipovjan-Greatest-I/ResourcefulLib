plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
    modApi "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveAppendix.set "$project.name-$minecraft_version"
    archiveClassifier.set "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveAppendix.set "$project.name-$minecraft_version"
    archiveClassifier.set null
}

jar {
    archiveAppendix.set "$project.name-$minecraft_version"
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "$archives_base_name-$project.name-$minecraft_version"
            from components.java

            pom {
                name = 'ResourcefulLib'
                description = 'The library behind Team Resourceful mods and more.'
                url = 'https://github.com/Resourceful-Bees/ResourcefulLib'

                scm {
                    connection = 'git:https://github.com/Resourceful-Bees/ResourcefulLib.git'
                    developerConnection = 'git:https://github.com/Resourceful-Bees/ResourcefulLib.git'
                    url = 'https://github.com/Resourceful-Bees/ResourcefulLib'
                }

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = repoUrl
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}
