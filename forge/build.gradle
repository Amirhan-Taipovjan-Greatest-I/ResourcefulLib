plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    include implementation("com.teamresourceful:yabn:${rootProject.yabn_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifactId = "$archives_base_name-forge-$minecraft_version"

            pom {
                name = 'ResourcefulLib'
                description = 'The library behind Team Resourceful mods and more.'
                url = 'https://github.com/Resourceful-Bees/ResourcefulLib'

                scm {
                    connection = 'git:https://github.com/Resourceful-Bees/ResourcefulLib.git'
                    developerConnection = 'git:https://github.com/Resourceful-Bees/ResourcefulLib.git'
                    url = 'https://github.com/Resourceful-Bees/ResourcefulLib'
                }

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://maven.resourcefulbees.com/repository/maven-releases/"
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}
